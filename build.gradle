plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.joonsang.graylog'
version = '1.0.4'

sourceCompatibility = 11

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'graylog-query-builder'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Graylog Query Builder'
                description = 'Graylog Search Query Builder'
                url = 'https://github.com/debugrammer/graylog-query-builder'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'debugrammer'
                        name = 'Joonsang Park'
                        email = 'debugrammer@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/debugrammer/graylog-query-builder.git'
                    developerConnection = 'scm:git:ssh:git@github.com:debugrammer/graylog-query-builder.git'
                    url = 'https://github.com/debugrammer/graylog-query-builder'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"

            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.3'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.0'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
}

test {
    useJUnitPlatform()
}
